plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
}

kotlin {
    jvmToolchain(17)
}

android {
    namespace = 'com.brianhenning.cribbage'
    compileSdk = 36

    defaultConfig {
        applicationId = "com.brianhenning.cribbage"
        minSdk = 24
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    configurations.all {
        resolutionStrategy {
            force 'androidx.test.ext:junit:1.2.1'
            force 'androidx.test.espresso:espresso-core:3.6.1'
        }
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("CRIBBAGE_KEYSTORE_PATH") ?: "${System.properties['user.home']}/.android/keystores/cribbage-release-key.jks")
            storePassword System.getenv("CRIBBAGE_KEYSTORE_PASSWORD")
            keyAlias System.getenv("CRIBBAGE_KEY_ALIAS") ?: "cribbage-release"
            keyPassword System.getenv("CRIBBAGE_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled = true // Enable ProGuard/R8 for release
            shrinkResources = true // Remove unused resources
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    lint {
        abortOnError = false
        disable 'WrongStartDestinationType'
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
        freeCompilerArgs += ["-Xskip-metadata-version-check"]
    }

    buildFeatures {
        compose = true
        viewBinding = true
    }
}

dependencies {
    // Core libraries - Latest versions compatible with compileSdk 36
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.compose.ui:ui-test-junit4-android:1.7.8'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    implementation "androidx.navigation:navigation-fragment-ktx:2.8.6"
    implementation "androidx.navigation:navigation-ui-ktx:2.8.6"

    // Compose - Using 2025.01.00 BOM (compatible with Kotlin 2.0+ and compileSdk 36)
    implementation platform('androidx.compose:compose-bom:2025.01.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.activity:activity-compose:1.10.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.8.7'
    implementation 'androidx.navigation:navigation-compose:2.8.6'
    implementation 'androidx.compose.material:material-icons-extended'

    // Compose Preview
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
